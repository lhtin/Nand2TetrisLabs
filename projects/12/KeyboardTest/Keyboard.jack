// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int kbAddr;
    /** Initializes the keyboard. */
    function void init() {
        let kbAddr = 24576;
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var int key;
        let key = Memory.peek(kbAddr);
        if ((key > 0) & (key < 153)) {
            return key;
        } else {
            return 0;
        }
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var int key1, key2;
        let key1 = Keyboard.keyPressed();
        while (key1 = 0) {
            let key1 = Keyboard.keyPressed();
        }
        let key2 = Keyboard.keyPressed();
        while (~(key2 = 0)) {
            let key2 = Keyboard.keyPressed();
        }
        do Output.printChar(key1);
        return key1;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var int len;
        var char c;
        var String str;
        let len = 0;
        let str = String.new(100);

        do Output.printString(message);
        let c = Keyboard.readChar();
        while (~(c = String.newLine())) {
            if (c = String.backSpace()) {
                if (len > 0) {
                    let len = len - 1;
                    do str.eraseLastChar();
                    // do Output.backSpace();
                }
            } else {
                let len = len + 1;
                do str.appendChar(c);
            }
            let c = Keyboard.readChar();
        }
        do Output.println();
        return str;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String str;
        let str = String.new(100);

        let str = Keyboard.readLine(message);
        return str.intValue();
    }
}
