// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
    // A类指令选择instruction，C类指令选择ALU的计算结果
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=AIn);
    
    // 为A类指令 或者 C类指令且d1=1
    Not(in=instruction[15], out=isA);
    Or(a=isA, b=instruction[5], out=ALoad);
    ARegister(in=AIn, load=ALoad, out=AOut, out[0..14]=addressM);
    
    // 为C类指令且d2=1
    And(a=instruction[15], b=instruction[4], out=DLoad);
    DRegister(in=ALUOut, load=DLoad, out=DOut);
    
    // 为C类指令且a=1
    And(a=instruction[15], b=instruction[12], out=sel1);
    Mux16(a=AOut, b=inM, sel=sel1, out=ALUIn2);
    
    And(a=instruction[15], b=instruction[11], out=zx);
    And(a=instruction[15], b=instruction[10], out=nx);
    And(a=instruction[15], b=instruction[9], out=zy);
    And(a=instruction[15], b=instruction[8], out=ny);
    And(a=instruction[15], b=instruction[7], out=f);
    And(a=instruction[15], b=instruction[6], out=no);
    ALU(x=DOut, y=ALUIn2, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, out=outM, out=ALUOut, ng=ng, zr=zr);
    
    // C指令且d3=1
    And(a=instruction[15], b=instruction[3], out=writeM);
    
    // j1&ng
    And(a=instruction[2], b=ng, out=lt);
    // j2&zr
    And(a=instruction[1], b=zr, out=eq);
    // j3&!(ng|zr)
    Or(a=ng, b=zr, out=t1);
    Not(in=t1, out=postive);
    And(a=instruction[0], b=postive, out=gt);
    Or(a=lt, b=eq, out=t2);
    Or(a=t2, b=gt, out=t3);
    And(a=instruction[15], b=t3, out=PCLoad);
    PC(in=AOut, load=PCLoad, inc=true, reset=reset, out[0..14]=pc);
}